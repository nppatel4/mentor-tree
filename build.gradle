buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.avast.gradle.docker-compose' version "0.8.3"
    id "com.github.pivotalservices.cf-app" version "2.2.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.solstice.mentor-and-tree'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = "Runs integration tests."
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

dockerCompose.isRequiredBy(integrationTest)

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.pivotal.spring.cloud:spring-cloud-services-dependencies:2.0.1.RELEASE'
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.cloud:spring-cloud-starter-openfeign:2.0.0.RELEASE')
    compile('io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry')
    compile('io.springfox:springfox-swagger2:2.9.2')
    compile('io.springfox:springfox-swagger-ui:2.9.2')

    compileOnly('org.projectlombok:lombok')

    runtime('com.h2database:h2:1.4.197')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('javax.xml.bind:jaxb-api')
}

def profiles = 'local'

bootRun {
    args = ["--spring.profiles.active=" + profiles]
}

cfConfig {
    ccHost = "api.run.pivotal.io"
    org = "solstice-org"

    name = project.name
    host = project.name
    filePath = "build/libs/" + name + "-" + project.version + ".jar"
    path = ""

    buildpack = "https://github.com/cloudfoundry/java-buildpack.git"
    environment = ["JAVA_OPTS": "-Djava.security.egd=file:/dev/./urandom", "SPRING_PROFILES_ACTIVE": "cloud"]
    services  = ["service-registry"]
}

dockerCompose {
    removeImages = "All"
}

integrationTest.doFirst {
    dockerCompose.exposeAsEnvironment(integrationTest)
}
